import java.text.SimpleDateFormat

buildscript {
  repositories {

    jcenter()
  }
  dependencies {
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
  }
}

plugins {
  id 'net.researchgate.release' version '2.0.2'
}


apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'


group = project.groupId
version = project.version

sourceCompatibility = project.javaVersion
targetCompatibility = project.javaVersion

repositories {
  jcenter()
}

dependencies {
  compile ("org.codehaus.groovy:groovy-all:${project.groovyVersion}")

  testCompile ("org.spockframework:spock-core:${project.spockVersion}")
}

Date buildTimeAndDate = new Date()
ext {
  buildDate = new SimpleDateFormat('dd-MMM-yyyy').format(buildTimeAndDate)
  buildTime = new SimpleDateFormat('hh:mm aa').format(buildTimeAndDate)
}

def jarManifestAttributes = [
  'Built-By'  : System.properties['user.name'],
  'Created-By': System.properties['java.version'] + ' (' + System.properties['java.vendor'] + ' ' + System.getProperty("java.vm.version") + ")",
  'Build-Date': buildDate,
  'Build-Time': buildTime]

jar {
  manifest {
    attributes(jarManifestAttributes)
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}


publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
}

bintray {
  user = project.hasProperty('bintray_user') ? bintray_user : ''
  key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

  publications = ['mavenJava']
  dryRun = false //Whether to run this as dry-run, without deploying
  publish = true //If version should be auto published after an upload
  pkg {
    repo = 'oss'
    userOrg = 'gextech'
    name = project.name
    desc = project.description
    websiteUrl = 'https://github.com/gextech/mapUtils'
    issueTrackerUrl = 'https://github.com/gextech/mapUtils/issues'
    vcsUrl = 'https://github.com/gextech/mapUtils.git'
    licenses = ['Apache-2.0']
    labels = ['gex', 'serling', 'map', 'utils']
    publicDownloadNumbers = true
    attributes = ['a': ['ay1', 'ay2'], 'b': ['bee'], c: 'cee']
    //Optional version descriptor
    version {
      name = project.version
      desc = project.description
      mavenCentralSync {
        sync = false
        user = ''
        password = ''
        close = '1'
      }
    }
  }
}


test {
  testLogging {
    exceptionFormat = 'full'
  }
}